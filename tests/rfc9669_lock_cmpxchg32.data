# Copyright (c) Microsoft Corporation
# SPDX-License-Identifier: MIT
-- asm
# RFC 9669 Section 5.3: atomic CMPXCHG (32-bit).
# A 32-bit atomic op updates 4 bytes at the target address. When the address
# points at an 8-byte little-endian word, this corresponds to the low 32 bits.

lddw %r0, 0x123456789abcdef0
stxdw [%r10-8], %r0

mov32 %r1, 0x87654321
mov32 %r0, 0x12345678
# Expected to fail (r0 doesn't match low 32-bits 0x9abcdef0).
lock cmpxchg32 [%r10-8], %r1

# %r0 contains old low 32-bits.
mov32 %r1, 0x9abcdef0
jne32 %r0, %r1, fail

# Memory is unmodified.
ldxdw %r2, [%r10-8]
lddw %r3, 0x123456789abcdef0
jne %r2, %r3, fail

lddw %r0, 0x123456789abcdef0
stxdw [%r10-8], %r0
mov32 %r1, 0x11223344
# Expected to succeed.
lock cmpxchg32 [%r10-8], %r1

# %r0 contains old low 32-bits.
mov32 %r2, 0x9abcdef0
jne32 %r0, %r2, fail

# Memory contains the expected 64-bit value.
ldxdw %r0, [%r10-8]
lddw %r1, 0x1234567811223344
jne %r0, %r1, fail

mov %r0, 1
exit
fail:
mov %r0, 0
exit
-- result
0x1
